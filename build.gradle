/*
 * Copyright (c) 2020 HPS
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
}

repositories {
    jcenter()
}

dependencies {
    api group: 'io.projectreactor', name: 'reactor-core', version: reactorCoreVersion
    testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: commonsVersion
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: groovyVersion
    testImplementation group: 'org.spockframework', name: 'spock-core', version: spockVersion
    testRuntime "net.bytebuddy:byte-buddy:1.9.3"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

publishing {
    publications {
        reactivePipelineLibrary(MavenPublication) {
            from components.java
            artifact sourceJar
            pom {
                name = "Reactive pipeline"
                description = "$description"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    connection = 'https://github.com/hpsworldwide/reactive-pipeline'
                    url = 'https://www.hps-worldwide.com/'
                }
            }
        }
    }
    bintray {
        user = System.getProperty('bintray.user')
        key = System.getProperty('bintray.key')
        publications = ['reactivePipelineLibrary']
        publish = true

        pkg {
            repo = 'hps-tools'
            name = 'reactive-pipelines'
            userOrg = 'hps-worldwide'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/hpsworldwide/reactive-pipeline.git'
            version {
                name = version
                desc = description
                released  = new Date()
            }
        }

    }
}

